(** PUBLIC **)

(**
	Handler:
		ORNAppCredential.login (username, password) 
	Description: 
		Enter Username (if needed) and password into login screen then attempts to login using credentials
	Parameters:
		username - user name needed to sign into application after selecting badge.  Can be passed in empty if shared badge
   		password - password of user
	Returns: 
		n/a
**)
to login username, password
	set loginArea to ORNAppCredential.getLoginArea
	WaitFor ORNInterval.medium, text:"Remember Username", SearchRectangle: loginArea
	put EveryImageLocation(text:"Username", SearchRectangle: loginArea) into usernameCount
	if number of items in userNameCount greater than 1 // For shared badge username is on screen twice
		click item 1 of userNameCount
		log "Attempting to login as user:" && username
		TypeText username
		if SutInfo().device_os_version equals "iOS 13.3.1"
			wait 60 second // iOS 13.3 bug on login screen
		end if
		Click text:"Password"
		TypeText Password
		if SutInfo().device_os_version equals "iOS 13.3.1"
			wait 60 second // iOS 13.3 bug on login screen
		end if
		TypeText returnkey
	else if imageFound (text:username, SearchRectangle:loginArea) // personal badge scenario where user is already filled in
		Click text:"Password"
		log "Attempting to enter password and login"
		TypeText Password
		if SutInfo().device_os_version equals "iOS 13.3.1"
			wait 60 second // iOS 13.3 bug on login screen
		end if
		TypeText returnkey
	else
		throw Name: "ORNAppCredential.login", reason: "Unable to interact with login screen"
	end if
	
	//if Invalid credentials are passed in
	Log "Checking for invalid username/password message"
	if imageFound (ORNInterval.small, text:"Invalid Username", SearchRectangle: loginArea)
		throw Name: "ORNAppCredential.login", reason:  "Invalid username or password"
	end if
end login

(**
	Handler:
		ORNAppCredential.onLoginScreenReturnToBadgeMobile
	Description: 
		 Determing device type and attempt to return to badge screen.
	Parameters: 
		n/a
	Returns: 
		n/a
**)
to onLoginScreenReturnToBadgeMobile
	log "Attempting to leave login screen and return to badge screen"
	if SutInfo().device_type start with "iP"
		ORNAppCredential.onLoginScreenReturnToBadgeiOS		
	else if SutInfo().device_os start with "Android"  
		ORNAppCredential.onLoginScreenReturnToBadgeAndroid
	end if
end onLoginScreenReturnToBadgeMobile


(** PRIVATE **)

(**
	Handler:
		ORNAppCredential.onLoginScreenReturnToBadgeiOS
	Description: 
		 Return to badge from Login on iOS device.
	Parameters:
		n/a
	Returns: 
		n/a
**)
to onLoginScreenReturnToBadgeiOS
	set searchArea to ORNScreen.scale (0, 0, .5, .2)
	log "Attempting to select any Change or Log Out options to return to badge screen"
	
	if imageFound(text:"Change", SearchRectangle: searchArea) // Change displayed on session end or if only one badge exist
		click FoundImageInfo().ImageRectangle.bottomCenter
	else if imageFound(text:"Log Out") 
		click FoundImageInfo().ImageRectangle.bottomCenter
		wait 2 // allow time for menu to slide in at bottom
		if imageFound(text:"Log Out") // If on PIN screen vs session end you might have to select Log out twice
			click last item in EveryImageLocation(text: "Log Out")
		end if

		if imageFound(text:"Change", SearchRectangle:searchArea) // Even after logging out, if only one badge you are still on credential screen, this is final check to return back to badge
			click FoundImageInfo().ImageRectangle.bottomCenter
		end if
	else
		throw Name: "ORNAppCredential.onLoginScreenReturnToBadgeiOS", reason:  "Unable to navigate to mobile badge screen"
	end if
end onLoginScreenReturnToBadgeiOS

(**
	Handler:
		ORNAppCredential.onLoginScreenReturnToBadgeiOS
	Description: 
		 Return to badge from Login on Android device
	Parameters: 
		n/a
	Returns: 
		n/a
**)
to onLoginScreenReturnToBadgeAndroid
	log "Attempting to select any Change or Log Out options to return to badge screen"
	set menuArea to ORNScreen.scale (.7, 0, 1, .3)
	
	if imageFound("MBLUtilities/Button_AndroidOverflowMenu") // open menu for logout or change option
		click FoundImageLocation()
	end if
	
	if imageFound(text:"Change Location", SearchRectangle: menuArea)
		click  FoundImageLocation()
	else if imageFound(text:"Log Out")
		click  FoundImageLocation()
		if imageFound(text:"Log Out") // If on PIN screen vs session end you might have to select Log out twice
			click last item in EveryImageLocation(text: "Log Out")
		end if	
	else
		throw Name: "ORNAppCredential.onLoginScreenReturnToBadgeAndroid", reason:  "Unable to navigate to mobile badge screen"
	end if
end onLoginScreenReturnToBadgeAndroid

(**
	Handler:
		ORNAppCredential.getLoginArea
	Description: 
		 Return to login area based on device type
	Parameters: 
		n/a
	Returns: 
		searchArea -  list, Coordinates used for search area rectangle
**)
to getLoginArea
	if SutInfo().device_type starts with "iPhone"  
		return ORNScreen.scale (.1, .1, .9, .9)
	else if SutInfo().device_type starts with "iPad"  
		return ORNScreen.scale (.1, .2, .9, .7)
	else if SutInfo().device_os start with "Android" 
		return ORNScreen.scale (.1, .1, .9, .9)
	else 
		return ORNScreen.fullScreen
	end if
end getLoginArea
